Program =
  { Statement } ;

Statement =
  Let |
  Struct |
  Enum |
  Import ;

Let =
  "let" , name , "=" , Expression ;

Struct =
  "struct" , name , "{" , [ Field , { "," , Field } , [ "," ]  ] , "}" ;

Field =
  name , ":" , name ;

Enum =
  "enum" , name , "{" , [ name , { "," , name } , [ "," ]  ] , "}" ;

Import =
  "import" , Path , ImportAlias , "{" , [ ImportItem , { "," , ImportItem } , [ "," ]  ] , "}" ;

ImportAlias = [ "as" , name ] ;

ImportItem = name , [ "as" , name ] ;

? Doesn't conflict with "Property" since "Path" is used exclusively within the "Import" statement ?
Path =
  name , { "." , name } ;

Expression =
  Abstraction |
  Bind |
  When |
  If |
  New |
  TypeCast |
  InfixLowest ;

Abstraction =
  name |
  ( name , "->" , Expression ) ;

Bind =
  "var" , name , "=" , Expression , "in" , Expression ;

? The first "|" is optional and recommended only for multiline "When" expressions ?
When =
  "when" , [ "|" ] , WhenBranch , { "|" , WhenBranch } , WhenDefault ;

WhenBranch =
  Expression , "then" , Expression ;

WhenDefault =
  "else" , Expression ;

If =
  "if" , Expression , "then" , Expression , "else" , Expression ;

New =
  name , "{" , [ SetField , { "," , SetField } , [ "," ]  ] , "}" ;

SetField =
  name , ":" , Expression ;

? All infix operators are right-associative ?

TypeCast =
  InfixLowest |
  ( InfixLowest , "::" , name ) ;

InfixLowest =
  InfixLower |
  ( InfixLower , "$" , InfixLowest ) ;

InfixLower =
  InfixLow |
  ( InfixLow , ( "and" | "or" ) , InfixLower ) ;

InfixLow =
  InfixHigh |
  ( InfixHigh , ( "==" | "!=" | "<=" | ">=" | "<" | ">" ) , InfixLow ) ;

InfixHigh =
  InfixHigher |
  ( InfixHigher , ( "+" | "-" ) , InfixHigh ) ;

InfixHigher =
  InfixHighest |
  ( InfixHighest , ( "*" | "/" | "%" ) , InfixHigher ) ;

InfixHighest =
  Application |
  ( Application , "^" , InfixHighest ) ;

Application =
  Property , { Property } ;

Property =
  Atom , { "." , name } ;

Atom =
  name |
  number |
  string |
  Parenthesized ;

Parenthesized =
  "(" , Expression , ")" ;

? Double "_" is not allowed ?
name =
  letter , { letter | digit } , [ "?" | "!" ] ;

letter =
  "a" .. "z" | "A" .. "Z" | "_" ;

? Trailing or double "_" is not allowed ?
number =
  [ "-" ] , digit , { digit | "_" } ;

digit =
  "0" .. "9" ;

string =
  '"' , { ? any char except quote ? } , '"' ;
